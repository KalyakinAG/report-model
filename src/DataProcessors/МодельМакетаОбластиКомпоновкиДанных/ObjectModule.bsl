//  Подсистема "Модель отчета"
//	Автор: Калякин Андрей Г.
//  https://github.com/KalyakinAG/kassl
//  https://github.com/KalyakinAG/report-model

Перем 
	МакетОбласти Экспорт,
	СтрокаТаблицы Экспорт,
	ЯчейкаТаблицы Экспорт,
	ПолеОбласти Экспорт,
	ОформлениеЯчейки Экспорт
;

Функция СтрокаТаблицы() Экспорт
	СтрокаТаблицы = МакетОбласти.Добавить(Тип("СтрокаТаблицыОбластиКомпоновкиДанных"));
	Возврат ЭтотОбъект;
КонецФункции

Функция ЯчейкаТаблицы() Экспорт
	ЯчейкаТаблицы = СтрокаТаблицы.Ячейки.Добавить();
	ОформлениеЯчейки = ЯчейкаТаблицы.Оформление;
	Возврат ЭтотОбъект;
КонецФункции

Функция ПолеОбласти(Значение, ФорматЗначения = Неопределено) Экспорт
	ПолеОбласти = ЯчейкаТаблицы.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
	ПолеОбласти.Значение = Значение;
	Если ЗначениеЗаполнено(ФорматЗначения) Тогда
		ПолеОбласти.Оформление.УстановитьЗначениеПараметра("Формат", ФорматЗначения);
	КонецЕсли;
	Возврат ЭтотОбъект;
КонецФункции

//	Для оформления ячейки таблицы используются следующие параметры оформления:
//	1. ЦветФона (BackColor) - тип значения Цвет;
//	2. ЦветТекста (TextColor) - тип значения Цвет;
//	3. ЦветГраницы (BorderColor) - тип значения Цвет;
//	4. СтильГраницы (BorderStyle) - тип значения Линия;
//	5. СтильГраницы.Слева (BorderStyle.Left) - тип значения Линия;
//	6. СтильГраницы.Сверху (BorderStyle.Top) - тип значения Линия;
//	7. СтильГраницы.Справа (BorderStyle.Right) - тип значения Линия;
//	8. СтильГраницы.Снизу (BorderStyle.Bottom) - тип значения Линия;
//	9. Шрифт (Font) - тип значения Шрифт;
//	10. Отступ (Indent) - тип значения Число;
//	11. ГоризонтальноеПоложение (HorizontalAlign) - тип значения ГоризонтальноеПоложение;
//	12. ВертикальноеПоложение (VerticalAlign) - тип значения ВертикальноеПоложение;
//	13. Размещение (Placement) - размещение текста - тип значения ТипРазмещенияТекстаКомпоновкиДанных;
//	14. ОриентацияТекста (TextOrientation) - тип значения Число;
//	15. ВыделятьОтрицательные (MarkNegatives) - тип значения Булево;
//	16. МинимальнаяШирина (MinWidth) - тип значения Число;
//	17. МаксимальнаяШирина (MaxWidth) - тип значения Число;
//	18. МинимальнаяВысота (MinHeight) - тип значения Число;
//	19. МаксимальнаяВысота (MaxHeight) - тип значения Число;
//	20. ОбъединятьПоГоризонтали (HorizontalMerge) - тип значения Булево;
//	21. ОбъединятьПоВертикали (VerticalMerge) - тип значения Булево;
//	22. ГоризонтальныйУровень (HorizontalLevel) - тип значения Число;
//	23. ВертикальныйУровень (VerticalLevel) - тип значения Число;
//	24. Расшифровка (Details) - тип ИдентификаторРасшифровкиКомпоновкиДанных;
//	25. РежимИзмененияРазмераКолонки (ColumnSizeChangeMode) - возможность изменять размер колонки ячейки РежимИзмененияРазмера;
//	26. ОтметкаНезаполненного (MarkIncomplete) - отмечать ячейку как незаполненную - тип значения Булево;
//	27. ФиксироватьСлева (FixLeft) - признак того, что ячейку нужно фиксировать слева Булево;
//	28. ФиксироватьСверху (FixTop) - признак того, что ячейку нужно фиксировать сверху Булево;
//	29. РастягиватьПоГоризонтали (HorizontalStretch) - тип значения Булево. Указывает, что ширина текущей колонки рассчитывается автоматически;
//	30. ВесовойКоэффициентШирины (WidthWeightFactor) - тип значения Число. Весовой коэффициент для автоматического расчета ширины колонки;
//	31. Картинка (Picture) - тип ПараметрКомпоновкиДанных или Картинка. Если содержит параметр, то картинка получается из выражения параметра.
//	32. РазмерКартинки (PictureSize) - тип РазмерКартинки. Способ определения размера картинки;
//	33. ПоложениеТекстаОтносительноКартинки (PictureSizeTextPositionRelativeToPicture) - тип ПоложениеТекстаОтносительноКартинки. Положение текста относительно картинки;
//	34. ГоризонтальноеПоложениеКартинки (PictureHorizontalAlign)- тип ГоризонтальноеПоложение. Размещение картинки по горизонтали;
//	35. ВертикальноеПоложениеКартинки (PictureVerticalAlign) - тип ВертикальноеПоложение. Размещение картинки по вертикали;
//	36. ОтступСлева (LeftMargin) - тип значения {ссылка:Объекты; 154; Число};
//	37. ОтступСправа (RightMargin) - тип значения {ссылка:Объекты; 154; Число};
//	38. ОтступСверху (TopMargin) - тип значения {ссылка:Объекты; 154; Число};
//	39. ОтступСнизу (BottomMargin) - тип значения {ссылка:Объекты; 154; Число}.
Функция Оформление(ИмяПараметра, Значение) Экспорт
	ОформлениеЯчейки.УстановитьЗначениеПараметра(ИмяПараметра, Значение);
	Возврат ЭтотОбъект;
КонецФункции

Функция Ширина(Значение) Экспорт
	Оформление("МинимальнаяШирина", Значение);
	Оформление("МаксимальнаяШирина", Значение);
	Возврат ЭтотОбъект;
КонецФункции

Функция Высота(Значение) Экспорт
	Оформление("МинимальнаяВысота", Значение);
	Оформление("МаксимальнаяВысота", Значение);
	Возврат ЭтотОбъект;
КонецФункции

Функция Объединять(НомерСтроки1, НомерКолонки1, НомерСтроки2, НомерКолонки2) Экспорт
	Для Строка = НомерСтроки1 По НомерСтроки2 Цикл
		СтрокаТаблицы = МакетОбласти[Строка - 1];
		Для Колонка = НомерКолонки1 По НомерКолонки2 Цикл
			ЯчейкаТаблицы = СтрокаТаблицы.Ячейки[Колонка - 1];
			ОформлениеЯчейки = ЯчейкаТаблицы.Оформление;  
			Если Колонка > НомерКолонки1 Тогда
				Оформление("ОбъединятьПоГоризонтали", Истина);
			КонецЕсли;							
			Если Строка > НомерСтроки1 Тогда
				Оформление("ОбъединятьПоВертикали", Истина);
			КонецЕсли;							
		КонецЦикла;
	КонецЦикла;
	Возврат ЭтотОбъект;
КонецФункции

МакетОбласти = Новый МакетОбластиКомпоновкиДанных;
